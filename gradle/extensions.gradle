/*
 * Copyright 2018 Kaidan Gustave
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
static Closure<String> dependencyExt(String group, String depName, String version, boolean modular = depName.endsWith('-')) {
    return modular ? { String module -> "$group:$depName$module:$version" } : { "$group:$depName:$version" }
}

ext {
    // DEPENDENCIES

    dependencies {
        kotlinLibrary = dependencyExt('org.jetbrains.kotlin', 'kotlin-', kotlinVersion)
        kotlinxCoroutines = dependencyExt('org.jetbrains.kotlinx', 'kotlinx-coroutines-', coroutinesVersion)
        kotlinxCoroutinesIo = dependencyExt('org.jetbrains.kotlinx', 'kotlinx-coroutines-io-', ioVersion)
        kotlinxIo = dependencyExt('org.jetbrains.kotlinx', 'kotlinx-io-', ioVersion)
        kotlinxAtomicFu = dependencyExt('org.jetbrains.kotlinx', 'atomicfu', atomicfuVersion)
        kotlinxSerialization = dependencyExt('org.jetbrains.kotlinx', 'kotlinx-serialization-', serializationVersion)
        ktor = dependencyExt('io.ktor', 'ktor-', ktorVersion)
        junitJupiter = dependencyExt('org.junit.jupiter', 'junit-jupiter-', junitJupiterVersion)
        spek = dependencyExt('org.spekframework.spek2', 'spek-', spekVersion)
        postgresql = dependencyExt('postgresql', 'postgresql', postgresqlVersion)
        slf4j = dependencyExt('org.slf4j', 'slf4j-api', slf4jVersion)
        logback = dependencyExt('ch.qos.logback', 'logback-classic', logbackVersion)
        jda = dependencyExt('net.dv8tion', 'JDA', jdaVersion)
        lavaplayer = dependencyExt('com.sedmelluq', 'lavaplayer', lavaplayerVersion)
        jdaUtilities = dependencyExt('com.jagrosh', 'jda-utilities-', jdaUtilitiesVersion)
        okHttp = dependencyExt('com.squareup.okhttp3', 'okhttp', okHttpVersion)
        hocon = dependencyExt('com.typesafe', 'config', hoconVersion)
        youtube = dependencyExt('com.google.apis', 'google-api-services-youtube', youtubeApiVersion)
        jsoup = dependencyExt('org.jsoup', 'jsoup', jsoupVersion)
        kotlinpoet = dependencyExt('com.squareup', 'kotlinpoet', kotlinpoetVersion)
        jagtag = dependencyExt('com.jagrosh', 'JagTag', jagtagVersion)
    }

    // REPOSITORIES

    repositories {
        jitpack = { maven { url 'https://jitpack.io/' } }
        sonatype = { maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } }
    }
}
